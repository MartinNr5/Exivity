buffer json_data = FILE system/extracted/json.json

csv OUTFILE = system/extracted/result.csv
csv add_headers OUTFILE account related_id type
csv fix_headers OUTFILE

foreach $JSON{json_data}.[data].[result] as this_result {

    # Extract the account name from each element in the 'result' array
    var account_name = $JSON(this_result).[account].[name]

    print Processing namespace: ${account_name}

    # Iterate over the metrics array within the result element
    foreach $JSON(this_result). [metrics] as this_metric {

    # As the metrics array contains anonymous arrays we need to iterate
    # further over each element. Note the use of an empty notepath.

        foreach $JSON (this_metric).[] as this_sub_metric {
            if(${this_sub_metric.COUNT} == 1) {
                # Assign the value on the first loop iteration to 'related_id'
                var related_id = $JSON(this_sub_metric).[]
            }
            if (${this_sub_metric.COUNT} == 2) {
                # Assign the value on the second loop iteration to 'type'
                var type = $JSON(this_sub_metric).[]
            }
        }

        csv write_fields OUTFILE ${account_name} ${related_id} ${type}
    }    
}
csv close OUTFILE

